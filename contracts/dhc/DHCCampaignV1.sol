// SPDX-License-Identifier: AGPL-3.0-or-later

pragma solidity ^0.8.0;

// OpenZeppelin Contracts v4.4.0 (utils/Context.sol)

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// OpenZeppelin Contracts v4.4.0 (access/Ownable.sol)

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// OpenZeppelin Contracts v4.4.0 (utils/Address.sol)

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// OpenZeppelin Contracts v4.4.0 (token/ERC20/IERC20.sol)

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// OpenZeppelin Contracts v4.4.0 (token/ERC20/utils/SafeERC20.sol)

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

library SafeMath {
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

interface IDiamondHandClub {
    function campaigns(address c) external view returns (bool);
    function wsGIZA() external view returns (address);
    function updatePenalty(uint value) external;
    function sendReward(address recipient, uint amount) external;
}

contract DHCCampaignV1 is Ownable {
    using SafeERC20 for IERC20;
    using SafeMath for uint;

    IDiamondHandClub public dhc;

    uint public minLockTime; // min lock time: s
    uint public penaltyPercent_; // 30 ~ 30/100 ~ 30%
    uint public rewardPeriod; // reward period of claim: s
    uint public rewardPercent; // 10000 ~ 10000/1000000 ~ 1%

    bool public withdrawEnabled = false;
    uint public endTime;
    bool private actived = false;

    mapping(address => UserInfo) public userInfo;
    mapping(address => bool) public privileges;

    uint public totalStaked;

    event Deposit(address indexed depositor, uint amount);
    event Withdraw(address indexed user, uint amount);
    event ClaimReward(address indexed recipient, uint amount, bool stake);

    struct UserInfo {
        uint staked;
        uint depositTime;
        uint nextClaimTime;
        bool locked;
    }

    modifier onlyActive() {
        require(isActive(), "DHCCampaignV1: ended");
        _;
    }

    modifier onlyPrivilege() {
        require(privileges[msg.sender], "DHCCampaignV1: only privilege");
        _;
    }

    constructor(
        address _dhc, 
        uint _minLockTime, 
        uint _penaltyPercent, 
        uint _rewardPeriod, 
        uint _rewardPercent, 
        uint _endTime
    ) {
        dhc = IDiamondHandClub(_dhc);
        setParams(_minLockTime, _penaltyPercent, _rewardPeriod, _rewardPercent);
        endTime = _endTime;
    }

    function setParams(
        uint _minLockTime, 
        uint _penaltyPercent, 
        uint _rewardPeriod, 
        uint _rewardPercent
    ) public onlyOwner {
        // require(minLockTime >= 518400, "min lock time must more than 6 days");
        minLockTime = _minLockTime;
        // require(penaltyPercent >= 30, "penalty percent must more than 30%");
        penaltyPercent_ = _penaltyPercent;
        // require(rewardPeriod >= 1800, "reward period must more than 0.5 hours");
        rewardPeriod = _rewardPeriod;
        // require(rewardPercent <= 50000, "reward percent must less than 5.0000%");
        rewardPercent = _rewardPercent;
    }

    function setEndTime(uint _endTime) external onlyOwner {
        endTime = _endTime;
    }

    function setActive(bool _actived) external onlyOwner {
        actived = _actived;
    }

    function lockUser(address _usr, bool _locked) external onlyOwner {
        UserInfo memory info = userInfo[_usr];

        userInfo[_usr] = UserInfo({
            staked: info.staked,
            depositTime: info.depositTime,
            nextClaimTime: info.nextClaimTime,
            locked: _locked
        });
    }

    function allowWithdraw(bool _en) external onlyOwner {
        withdrawEnabled = _en;
    }

    function addPrivilege(address p) external onlyOwner {
        privileges[p] = true;
    }

    function removePrivilege(address p) external onlyOwner {
        delete privileges[p];
    }



    function deposit(uint _amount) external onlyActive {
        require(!userInfo[msg.sender].locked, "DHCCampaignV1: depositor has been locked");
        require(_amount > 0, "DHCCampaignV1: invalid deposit amount");

        IERC20(dhc.wsGIZA()).safeTransferFrom(msg.sender, address(this), _amount);

        _deposit(_amount, msg.sender);
    }

    function depositWithPrivilege(
        uint _amount, 
        address _depositor
    ) external onlyActive onlyPrivilege {
        require(!userInfo[_depositor].locked, "DHCCampaignV1: depositor has been locked");
        require(_amount > 0, "DHCCampaignV1: invalid deposit amount");

        IERC20(dhc.wsGIZA()).safeTransferFrom(msg.sender, address(this), _amount);

        _deposit(_amount, _depositor);
    }

    function _deposit(uint _amount, address _depositor) private {
        UserInfo memory info = userInfo[_depositor];

        userInfo[_depositor] = UserInfo({
            staked: info.staked.add(_amount),
            depositTime: block.timestamp,
            nextClaimTime: getNextPeriod(block.timestamp),
            locked: info.locked
        });

        _updateDeposit(_amount);
        emit Deposit(_depositor, _amount);
    }

    function claim(bool _stake) external returns (uint _reward, uint _newNextClaimTime) {
        uint _currentTime = block.timestamp > endTime ? endTime : block.timestamp;
        (_reward, _newNextClaimTime) = getReward(
            userInfo[msg.sender].staked, 
            userInfo[msg.sender].nextClaimTime, 
            _currentTime
        );
        if (_reward > 0) {
            if (_stake) {
                dhc.sendReward(address(this), _reward);
                _deposit(_reward, msg.sender);
            } else {
                userInfo[msg.sender].nextClaimTime = _newNextClaimTime;
                userInfo[msg.sender].depositTime = block.timestamp;
                dhc.sendReward(msg.sender, _reward);
            }
            emit ClaimReward(msg.sender, _reward, _stake);
        }

        return (_reward, _newNextClaimTime);
    }

    function withdraw() external {
        require(userInfo[msg.sender].staked > 0, "DHCCampaignV1: nothing to withdraw");

        UserInfo memory info = userInfo[msg.sender];
        uint _staked = info.staked;
        _updateWithdraw(_staked);

        if (block.timestamp <= getUnlockTime(msg.sender)) {
            uint penalty = _staked.mul(penaltyPercent()).div(100);
            IERC20(dhc.wsGIZA()).safeTransfer(address(dhc), penalty);
            dhc.updatePenalty(penalty);
            _staked = _staked.sub(penalty);
        }

        IERC20(dhc.wsGIZA()).safeTransfer(msg.sender, _staked);
        delete userInfo[msg.sender];

        emit Withdraw(msg.sender, info.staked);
    }

    function _updateDeposit(uint value) private {
        totalStaked = totalStaked.add(value);
    }

    function _updateWithdraw(uint value) private {
        totalStaked = totalStaked.sub(value);
    }



    function getReward(
        uint _staked, 
        uint _nextClaimTime, 
        uint _currentTime
    ) public view returns (uint _reward, uint _newNextClaimTime) {
        if (_staked == 0) {
            return (0, _nextClaimTime);
        }
        
        if (_nextClaimTime > _currentTime) {
            return (0, _nextClaimTime);
        }
        uint _baseReward = _staked.mul(rewardPercent).div(1000000);
        uint _rewardTimes = _currentTime.sub(_nextClaimTime).div(rewardPeriod).add(1);
        _newNextClaimTime = _nextClaimTime.add(_rewardTimes.mul(rewardPeriod));
        _reward = _baseReward.mul(_rewardTimes);
        return (_reward, _newNextClaimTime);
    }

    function getNextPeriod(uint _timestamp) public view returns (uint) {
        return _timestamp.sub(_timestamp.mod(rewardPeriod)).add(rewardPeriod);
    }

    function getUnlockTime(address _user) public view returns (uint) {
        return userInfo[_user].depositTime.add(minLockTime);
    }

    function isActive() public view returns (bool) {
        return block.timestamp < endTime && actived && dhc.campaigns(address(this));
    }

    function penaltyPercent() public view returns (uint) {
        if (withdrawEnabled) {
            return 0;
        }
        return penaltyPercent_;
    }
}